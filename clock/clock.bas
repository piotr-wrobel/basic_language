100 CLS
110 INPUT "INPUT HOURS,MINUTES AND SECOND";CH,CM,CS
200 DIM T(1,59)
300 SCREEN 128
400 CX=160:CY=120:NL=105:SL=100:ML=70:HL=40:NC=60:SC=0:MC=12:HC=5:BC=1
500 FOR A=0 TO 59 STEP 1
550 REM WARNIG, WHEN YOU PASTE THE ~ SYMBOL
560 REM INTO X16 ON THE LINE BELOW, IT CHANGES TO PI SYMBOL
600 S=(A+45)*2*~/60
700 T(0,A)=COS(S):T(1,A)=SIN(S)
750 PX=CX+T(0,A)*NL:PY=CY+T(1,A)*NL
760 RECT PX-1,PY-1,PX+1,PY+1,NC:NC=NC+1
900 I=A:J=5:GOSUB 30000:B=I
1000 IF B=0 THEN GOSUB 50000
1100 NEXT
1200 REM CLOCK ENGINE
1300 TM=INT(CM/12)
1400 I=CH:J=12:GOSUB 30000:CH=(I*5)+TM
1500 LINE CX,CY,CX+T(0,CS)*SL,CY+T(1,CS)*SL,SC
1550 RECT CX-2,CY-2,CX+2,CY+2,7
1600 LINE CX,CY,CX+T(0,CM)*ML,CY+T(1,CM)*ML,MC
1700 LINE CX,CY,CX+T(0,CH)*HL,CY+T(1,CH)*HL,HC
1800 RECT CX-2,CY-2,CX+2,CY+2,7
1900 T=1:GOSUB 20000
2000 LINE CX,CY,CX+T(0,CS)*SL,CY+T(1,CS)*SL,BC
2100 IF CS=59 THEN GOSUB 40000
2200 I=CS+1:J=60:GOSUB 30000:CS=I
2300 IF CS=0 THEN I=CM+1:J=60:GOSUB 30000:CM=I
2400 I=CM:J=12:GOSUB 30000:TM=I
2500 IF TM=0 AND CS=0 THEN I=CH+1:J=60:GOSUB 30000:CH=I
2600 GOTO 1500
20000 T0=TI: FOR T=T0+T*59 TO T: T=TI: T=T-5184E3*(T<T0): NEXT: RETURN
30000 I=I-INT(I/J)*J: RETURN
40000 LINE CX,CY,CX+T(0,CM)*ML,CY+T(1,CM)*ML,BC
41000 LINE CX,CY,CX+T(0,CH)*HL,CY+T(1,CH)*HL,BC
42000 RETURN
50000 PX=CX+T(0,A)*(NL+5):PY=CY+T(1,A)*(NL+5):RECTPX-2,PY-2,PX+2,PY+2,NC+1
51000 RETURN
